buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext {
        guavaVersion = '30.1.1-jre'
        mapstructVersion = '1.4.2.Final'
        checkerFrameworkVersion = '3.12.0'

        lombokPluginVersion = '5.3.3.3'
        springBootPluginVersion = '2.4.3'
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
        checkerFrameworkPluginVersion = '0.5.19'
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:$lombokPluginVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
        classpath "org.checkerframework:checkerframework-gradle-plugin:$checkerFrameworkPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion"
    }
}

allprojects {
    group = 'com.hohomalls'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    if (it.name == 'hohomalls-app') {
        apply plugin: 'java'
    } else {
        apply plugin: 'java-library'
    }

    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.checkerframework'

    java.sourceCompatibility = JavaVersion.VERSION_11
    java.targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            }

            dependency group: 'com.google.guava', name: 'guava', version: guavaVersion
            dependency group: 'org.checkerframework', name: 'checker', version: checkerFrameworkVersion
            dependency group: 'org.checkerframework', name: 'checker-qual', version: checkerFrameworkVersion

            dependencySet(group: 'org.mapstruct', version: mapstructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }
        }
    }

    dependencies {
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'
        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'org.mapstruct', name: 'mapstruct'
        implementation group: 'org.checkerframework', name: 'checker'
        implementation group: 'org.checkerframework', name: 'checker-qual'
        testImplementation group: 'io.projectreactor', name: 'reactor-test'
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }
}

