buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    ext {
        // Dependency versions
        graphqlDgsVersion = '4.1.0'
        guavaVersion = '30.1.1-jre'
        jetbrainsAnnotationVersion = '21.0.1'
        jooqVersion = '0.9.14'
        mapstructVersion = '1.4.2.Final'

        // Plugin versions
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
        lombokPluginVersion = '5.3.3.3'
        springBootPluginVersion = '2.4.3'
        dgsCodegenPluginVersion = '4.6.4'
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
        classpath "io.freefair.gradle:lombok-plugin:$lombokPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion"
        classpath "com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-gradle:$dgsCodegenPluginVersion"
    }
}

allprojects {
    group = 'com.hohomalls'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    java.sourceCompatibility = JavaVersion.VERSION_11
    java.targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            }

            dependency group: 'com.google.guava', name: 'guava', version: guavaVersion
            dependency group: 'org.jetbrains', name: 'annotations', version: jetbrainsAnnotationVersion
            dependency group: 'org.jooq', name: 'jool', version: jooqVersion

            dependencySet(group: 'org.mapstruct', version: mapstructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }
        }
    }

    dependencies {
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'

        implementation(platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:$graphqlDgsVersion"))

        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'org.jetbrains', name: 'annotations'
        implementation group: 'org.jooq', name: 'jool'
        implementation group: 'org.mapstruct', name: 'mapstruct'

        testAnnotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'

        testImplementation group: 'io.projectreactor', name: 'reactor-test'
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }

    compileJava {
        options.compilerArgs += [
                '-Amapstruct.defaultComponentModel=spring',
                '-Amapstruct.defaultInjectionStrategy=constructor'
        ]
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }
}

