buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext {
        guavaVersion = '30.1.1-jre'
        mapstructVersion = '1.4.2.Final'

        junitVersion = '5.7.1'

        lombokPluginVersion = '6.0.0-m2'
        springBootPluginVersion = '2.4.3'
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:$lombokPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
    }
}

allprojects {
    group = 'com.hohomalls'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    java.sourceCompatibility = JavaVersion.VERSION_11
    java.targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        dependencies {
            dependency group: 'com.google.guava', name: 'guava', version: guavaVersion

            dependencySet(group: 'org.mapstruct', version: mapstructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }

            dependencySet(group: 'org.junit.jupiter', version: junitVersion) {
                entry 'junit-jupiter-api'
                entry 'junit-jupiter-engine'
            }
        }
    }

    dependencies {
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'
        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'org.mapstruct', name: 'mapstruct'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }
}

