buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    ext {
        // Dependency versions
        apacheValidatorVersion = '1.7'
        graphqlDgsVersion = '4.4.0'
        jetbrainsAnnotationVersion = '21.0.1'
        jjwtVersion = '0.11.2'
        jooqVersion = '0.9.14'
        mapstructVersion = '1.4.2.Final'
        reactorVersion = '3.4.8'

        // Plugin versions
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
        lombokPluginVersion = '5.3.3.3'
        springBootPluginVersion = '2.5.3'
        dgsCodegenPluginVersion = '5.0.3'

        // Configuration versions
        checkstyleToolVersion = '8.44'
        jacocoToolVersion = '0.8.7'
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
        classpath "io.freefair.gradle:lombok-plugin:$lombokPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion"
        classpath "com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-gradle:$dgsCodegenPluginVersion"
    }
}

allprojects {
    group = 'com.hohomalls'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    // Plugin order matters
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    java.sourceCompatibility = JavaVersion.VERSION_11
    java.targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        // Three ways to manage dependency version numbers:
        // 1. By using versions from SpringBootPlugin.BOM_COORDINATES in the imports block
        // 2. By defining versions in the ext block
        // 3. By using the platform of netflix dgs in the dependencies block
        dependencies {
            imports {
                // Import dependency version numbers
                mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            }

            dependency group: 'commons-validator', name: 'commons-validator', version: apacheValidatorVersion
            dependency group: 'org.jetbrains', name: 'annotations', version: jetbrainsAnnotationVersion
            dependency group: 'org.jooq', name: 'jool', version: jooqVersion
            dependency group: 'io.projectreactor', name: 'reactor-tools', version: reactorVersion
            dependency group: 'com.netflix.graphql.dgs', name: 'graphql-dgs-platform-dependencies', version: graphqlDgsVersion

            dependencySet(group: 'org.mapstruct', version: mapstructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }

            dependencySet(group: 'io.jsonwebtoken', version: jjwtVersion) {
                entry 'jjwt-api'
                entry 'jjwt-impl'
                entry 'jjwt-jackson'
            }
        }
    }

    dependencies {
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'
        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'org.jetbrains', name: 'annotations'
        implementation group: 'org.jooq', name: 'jool'
        implementation group: 'org.mapstruct', name: 'mapstruct'
        implementation(platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies"))
        testAnnotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'
        testImplementation group: 'io.projectreactor', name: 'reactor-test'
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }

    compileJava {
        options.compilerArgs += [
                '-Amapstruct.defaultComponentModel=spring',
                '-Amapstruct.defaultInjectionStrategy=constructor'
        ]
    }

    test {
        useJUnitPlatform()

        // report is always generated after tests run
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = jacocoToolVersion
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    // FIXME Update the ratio
                    minimum = 0.1
                }
            }
        }
    }

    jacocoTestReport {
        // tests are required to run before generating the report
        dependsOn test

        reports {
            xml.required = false
            csv.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['com/hohomalls/**/config/*',
                                            'com/hohomalls/**/scalar/*',
                                            'com/hohomalls/**/pojo/*',
                                            'com/hohomalls/**/document/*',
                                            'com/hohomalls/**/mapper/*',
                                            'com/hohomalls/**/property/*'])
            }))
        }
    }

    // Run coverage verification during the build (and fail when appropriate)
    check.dependsOn jacocoTestCoverageVerification

    checkstyle {
        maxErrors = 0
        maxWarnings = 0
        ignoreFailures = false
        showViolations = true
        toolVersion = checkstyleToolVersion
        // Skip test cases
        sourceSets = [project.sourceSets.main]
        checkstyleMain {
            // Get rid of the gradle warning from the plugin itself
            dependsOn compileTestJava
            // Excludes auto generated graphql and mapstruct classes
            excludes = ['**/graphql/**', '**/mapper/*Impl*']
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }
}


