buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    ext {
        // Dependency versions
        guavaVersion = '30.1.1-jre'
        mapstructVersion = '1.4.2.Final'
        jooqVersion = '0.9.14'
        graphqlKickstartVersion = '11.0.0'

        // Plugin versions
        dependencyManagementPluginVersion = '1.0.11.RELEASE'
        lombokPluginVersion = '5.3.3.3'
        springBootPluginVersion = '2.4.3'
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementPluginVersion"
        classpath "io.freefair.gradle:lombok-plugin:$lombokPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion"
    }
}

allprojects {
    group = 'com.hohomalls'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    java.sourceCompatibility = JavaVersion.VERSION_11
    java.targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            }

            dependency group: 'com.google.guava', name: 'guava', version: guavaVersion
            dependency group: 'org.jooq', name: 'jool', version: jooqVersion

            dependencySet(group: 'com.graphql-java-kickstart', version: graphqlKickstartVersion) {
                entry 'graphql-kickstart-spring-boot-starter-webflux'
                entry 'graphql-kickstart-spring-boot-starter-tools'
            }

            dependencySet(group: 'org.mapstruct', version: mapstructVersion) {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }
        }
    }

    dependencies {
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor'
        implementation group: 'com.google.guava', name: 'guava'
        implementation group: 'org.mapstruct', name: 'mapstruct'
        implementation group: 'org.jooq', name: 'jool'

        testImplementation group: 'io.projectreactor', name: 'reactor-test'
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }
}

