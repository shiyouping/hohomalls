// See guideline at https://docs.gradle.org/current/userguide/java_platform_plugin.html
plugins {
    id 'java-platform'
}

ext {
    versions = ['awspring'        : '2.4.1',
                'caffeine'        : '3.0.6',
                'commonsBeanutils': '1.9.4',
                'commonsValidator': '1.7',
                'dgsFramework'    : '4.10.2',
                'guava'           : '31.1-jre',
                'jetbrains'       : '23.0.0',
                'jjwt'            : '0.11.2',
                'libphonenumber'  : '8.12.46',
                'mapstruct'       : '1.4.2.Final',
                'mongock'         : '5.0.38',
                'netty'           : '4.1.75.Final',
                'passay'          : '1.6.1',
                'springBoot'      : '2.6.7',
                'springCloud'     : '2021.0.2']
}

javaPlatform {
    // Allow to include the constraints from third-party BOMs
    allowDependencies()
}

dependencies {
    // Import versions from other BOMs
    api platform("io.awspring.cloud:spring-cloud-aws-dependencies:$versions.awspring")
    api platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:$versions.dgsFramework")
    api platform("org.springframework.boot:spring-boot-dependencies:$versions.springBoot")
    api platform("org.springframework.cloud:spring-cloud-dependencies:$versions.springCloud")

    constraints {
        api "com.github.ben-manes.caffeine:caffeine:$versions.caffeine"
        api "com.googlecode.libphonenumber:libphonenumber:$versions.libphonenumber"
        api "com.google.guava:guava:$versions.guava"
        api "commons-beanutils:commons-beanutils:$versions.commonsBeanutils"
        api "commons-validator:commons-validator:$versions.commonsValidator"
        api "io.jsonwebtoken:jjwt-api:$versions.jjwt"
        api "io.mongock:mongock-springboot:$versions.mongock"
        api "io.mongock:mongodb-springdata-v3-driver:$versions.mongock"
        api "io.netty:netty-all:$versions.netty"
        api "org.jetbrains:annotations:$versions.jetbrains"
        api "org.mapstruct:mapstruct-processor:$versions.mapstruct"
        api "org.mapstruct:mapstruct:$versions.mapstruct"
        api "org.passay:passay:$versions.passay"

        runtime "io.jsonwebtoken:jjwt-impl:$versions.jjwt"
        runtime "io.jsonwebtoken:jjwt-jackson:$versions.jjwt"
    }
}