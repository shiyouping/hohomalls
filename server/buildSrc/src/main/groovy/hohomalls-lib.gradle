// Custom plugin for Java library
plugins {
    // Plugin order matters
    id 'java-library'
    id 'io.freefair.lombok'
    id 'jacoco'
    id 'pmd'
    id 'checkstyle'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    // report is always generated after tests run
    finalizedBy jacocoTestReport
}

check {
    // Run coverage verification during the build (and fail when appropriate)
    dependsOn jacocoTestCoverageVerification
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                // FIXME
                minimum = 0
            }
        }
    }
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test

    reports {
        xml.required = false
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['com/hohomalls/**/config/*',
                                        'com/hohomalls/**/scalar/*',
                                        'com/hohomalls/**/pojo/*',
                                        'com/hohomalls/**/document/*',
                                        'com/hohomalls/**/mapper/*',
                                        'com/hohomalls/**/property/*',
                                        'com/hohomalls/**/graphql/*'])
        }))
    }
}

checkstyle {
    // The Checkstyle IDE plugin should use the some rule
    // files stored in config/checkstyle/checkstyle.xml
    maxErrors = 0
    maxWarnings = 10
    ignoreFailures = false
    showViolations = true
    toolVersion = '8.44'
    // Skip test cases
    sourceSets = [project.sourceSets.main]
    checkstyleMain {
        // Get rid of the gradle warning from the plugin itself
        dependsOn compileTestJava
        excludes = ['**/graphql/**', '**/mapper/*Impl*']
    }
}

pmd {
    maxFailures = 0
    consoleOutput = false
    ignoreFailures = false
    incrementalAnalysis = false
    toolVersion = '6.36.0'
    // Skip test cases, which was also defined in the rule set file
    sourceSets = [project.sourceSets.main]
    // The PMD IDE plugin should use these rule sets as well.
    ruleSetFiles "$rootDir/config/pmd/java.xml"
    // Disable built-in rule sets
    ruleSets = []

    pmdMain {
        // Excluded rules are defined in the rule set file
        dependsOn compileTestJava
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}